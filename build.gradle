plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.30'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'java'
}

group 'com.dominikkorsa.tablistworld'
version '1.0.0'

repositories {
    mavenCentral()
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots' }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    compileOnly "org.spigotmc:spigot-api:1.8-R0.1-SNAPSHOT"
    implementation 'org.bstats:bstats-bukkit:3.0.0'
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
shadowJar {
    relocate 'org.bstats', 'com.dominikkorsa.tablistworld'
    archiveBaseName.set("TablistWorld")
    archiveClassifier.set('')
}
